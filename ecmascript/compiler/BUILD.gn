# Copyright (c) 2021 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

if (defined(ark_independent_build)) {
  import("//js_runtime/js_runtime_config.gni")
  import("$build_root/ark.gni")
} else {
  import("//ark/js_runtime/js_runtime_config.gni")
  import("//build/ohos.gni")

  action("build_llvm_libs") {
    script = "compile_llvm_lib.sh"
    sources = [ "$js_root/ecmascript/compiler/compile_llvm_lib.sh" ]
    outputs = [ "${root_out_dir}/llvm" ]
  }
}

config("include_llvm") {
  if (compile_llvm_online) {
    include_dirs = [
      "//third_party/llvm-project/build/include",
      "//third_party/llvm-project/llvm/include/",
    ]
  } else {
    include_dirs = [
      "//prebuilts/ark_tools/ark_js_prebuilts/llvm_prebuilts/llvm/include",
      "//prebuilts/ark_tools/ark_js_prebuilts/llvm_prebuilts/build/include",
    ]
  }
}

source_set("libark_jsoptimizer_set") {
  sources = [
    "aot_file_manager.cpp",
    "assembler/aarch64/assembler_aarch64.cpp",
    "assembler/aarch64/extend_assembler.cpp",
    "assembler/x64/assembler_x64.cpp",
    "assembler/x64/extended_assembler_x64.cpp",
    "assembler_module.cpp",
    "bc_call_signature.cpp",
    "bytecode_circuit_builder.cpp",
    "call_signature.cpp",
    "circuit.cpp",
    "circuit_builder.cpp",
    "common_stubs.cpp",
    "gate.cpp",
    "gate_accessor.cpp",
    "interpreter_stub.cpp",
    "llvm_codegen.cpp",
    "llvm_ir_builder.cpp",
    "rt_call_signature.cpp",
    "scheduler.cpp",
    "stub.cpp",
    "test_stubs.cpp",
    "test_stubs_signature.cpp",
    "trampoline/aarch64/assembler_stubs.cpp",
    "trampoline/x64/assembler_stubs_x64.cpp",
    "type.cpp",
    "verifier.cpp",
  ]

  public_configs = [
    ":include_llvm",
    "$js_root:ark_jsruntime_compiler_config",
    "$js_root:ark_jsruntime_public_config",
  ]

  if (compile_llvm_online) {
    lib_dirs = [ "//third_party/llvm-project/build/lib" ]
  } else {
    lib_dirs =
        [ "//prebuilts/ark_tools/ark_js_prebuilts/llvm_prebuilts/build/lib" ]
  }

  libs = [
    "LLVMTarget",
    "LLVMObject",
    "LLVMMC",
    "LLVMSupport",
    "LLVMCore",
    "LLVMExecutionEngine",
    "LLVMInterpreter",
    "LLVMMCJIT",
    "LLVMExegesis",
    "LLVMRuntimeDyld",
    "LLVMInstCombine",
    "LLVMAnalysis",
    "LLVMScalarOpts",
    "LLVMBinaryFormat",
    "LLVMDebugInfoDWARF",
    "LLVMRemarks",
    "LLVMTextAPI",
    "LLVMScalarOpts",
    "LLVMTransformUtils",
    "LLVMBitReader",
    "LLVMAsmPrinter",
    "LLVMProfileData",
    "LLVMBitstreamReader",
    "LLVMSelectionDAG",
    "LLVMGlobalISel",
    "LLVMLTO",
    "LLVMCFGuard",
    "LLVMVectorize",
    "LLVMDemangle",
    "LLVMipo",
    "LLVMInstrumentation",
    "LLVMDebugInfoCodeView",
    "LLVMAggressiveInstCombine",
    "LLVMAsmParser",
    "LLVMMCParser",
    "LLVMMIRParser",
    "LLVMMCDisassembler",
    "LLVMCodeGen",
    "LLVMTransformUtils",
    "LLVMIRReader",
    "LLVMARMUtils",
    "LLVMARMCodeGen",
    "LLVMARMDisassembler",
    "LLVMARMDesc",
    "LLVMARMInfo",
    "LLVMARMAsmParser",
    "LLVMAArch64Utils",
    "LLVMAArch64CodeGen",
    "LLVMAArch64Info",
    "LLVMAArch64Desc",
    "LLVMAArch64Disassembler",
    "LLVMAArch64AsmParser",
    "LLVMX86Utils",
    "LLVMX86AsmParser",
    "LLVMX86CodeGen",
    "LLVMX86Desc",
    "LLVMX86Disassembler",
    "LLVMX86Info",
  ]

  deps = [
    "$ark_root/libpandabase:libarkbase",
    "$ark_root/libpandafile:libarkfile",
  ]

  if (compile_llvm_online) {
    deps += [ ":build_llvm_libs" ]
  }
}

source_set("ark_stub_compiler_set") {
  sources = [ "stub_compiler.cpp" ]
  include_dirs = [ "$target_gen_dir" ]

  public_configs = [
    ":include_llvm",
    "$js_root:ark_jsruntime_compiler_config",
    "$js_root:ark_jsruntime_public_config",
  ]

  deps = [
    "$ark_root/libpandabase:libarkbase",
    "$js_root:libark_jsruntime",
    "$js_root/ecmascript/compiler:libark_jsoptimizer",
  ]
}

source_set("ark_aot_compiler_set") {
  sources = [
    "aot_compiler.cpp",
    "pass_manager.cpp",
    "slowpath_lowering.cpp",
  ]

  public_configs = [
    ":include_llvm",
    "$js_root:ark_jsruntime_compiler_config",
    "$js_root:ark_jsruntime_public_config",
  ]

  deps = [
    "$ark_root/libpandabase:libarkbase",
    "$js_root:libark_jsruntime",
    "$js_root/ecmascript/compiler:libark_jsoptimizer",
  ]
}

if (!defined(ark_independent_build)) {
  ohos_shared_library("libark_jsoptimizer") {
    deps = [
      ":libark_jsoptimizer_set",
      "$js_root:libark_jsruntime",
    ]

    install_enable = false

    output_extension = "so"
    part_name = "ark_js_runtime"
    subsystem_name = "ark"
  }

  ohos_shared_library("libark_jsoptimizer_test") {
    deps = [
      ":libark_jsoptimizer_set",
      "$js_root:libark_jsruntime_test_set",
    ]

    install_enable = false

    output_extension = "so"
    subsystem_name = "test"
  }

  ohos_executable("ark_stub_compiler") {
    deps = [ ":ark_stub_compiler_set" ]

    install_enable = false

    part_name = "ark_js_runtime"
    subsystem_name = "ark"
  }

  ohos_executable("ark_aot_compiler") {
    deps = [ ":ark_aot_compiler_set" ]

    install_enable = false

    part_name = "ark_js_runtime"
    subsystem_name = "ark"
  }
  action("gen_stub_file") {
    script = "$js_root/script/run_ark_executable.py"

    deps =
        [ "$js_root/ecmascript/compiler:ark_stub_compiler(${host_toolchain})" ]

    stub_file_gen_dir = "$root_gen_dir/ark/js_runtime"

    stub_option = " --com-stub-out=" + rebase_path(stub_file_gen_dir) +
                  "/com_stub.m --bc-stub-out=" +
                  rebase_path(stub_file_gen_dir) + "/bc_stub.m"

    args = [
      "--script-file",
      rebase_path(root_out_dir) + "/ark/ark_js_runtime/ark_stub_compiler",
      "--script-options",
      stub_option,
      "--expect-output",
      "0",
      "--env-path",
      rebase_path(root_out_dir) + "/ark/ark:" + rebase_path(root_out_dir) +
          "/ark/ark_js_runtime:" + rebase_path(root_out_dir) +
          "/thirdparty/icu:" +
          rebase_path("//prebuilts/clang/ohos/linux-x86_64/llvm/lib/"),
    ]

    outputs = [
      "$stub_file_gen_dir/com_stub.m",
      "$stub_file_gen_dir/bc_stub.m",
    ]
  }
} else {
  ark_shared_library("libark_jsoptimizer") {
    deps = [
      ":libark_jsoptimizer_set",
      "$js_root:libark_jsruntime",
    ]
    output_extension = "so"
  }
}

config("gen_stub_file_dir_config") {
  stub_file_gen_dir = rebase_path(root_gen_dir) + "/ark/js_runtime"
  defines = [ "STUB_FILE_GEN_DIR=\"${stub_file_gen_dir}/\"" ]
}
