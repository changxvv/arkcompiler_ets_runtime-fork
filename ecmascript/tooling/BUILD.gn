# Copyright (c) 2021-2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//ark/js_runtime/js_runtime_config.gni")
import("//build/ohos.gni")

config("ark_ecma_debugger_config") {
  configs = [
    "//ark/js_runtime:ark_jsruntime_common_config",
    "//ark/js_runtime:ark_jsruntime_public_config",
  ]

  include_dirs = [
    "//ark/js_runtime/ecmascript/tooling",
    "//third_party/boost",
  ]
}

debugger_sources = [
  "agent/debugger_impl.cpp",
  "agent/heapprofiler_impl.cpp",
  "agent/js_backend.cpp",
  "agent/js_pt_hooks.cpp",
  "agent/profiler_impl.cpp",
  "agent/runtime_impl.cpp",
  "base/pt_events.cpp",
  "base/pt_params.cpp",
  "base/pt_returns.cpp",
  "base/pt_script.cpp",
  "base/pt_types.cpp",
  "debugger_service.cpp",
  "dispatcher.cpp",
  "js_pt_extractor.cpp",
  "protocol_handler.cpp",
]

source_set("libark_ecma_debugger_static") {
  sources = debugger_sources

  public_configs = [ ":ark_ecma_debugger_config" ]

  deps = [
    "$ark_root/libpandabase:libarkbase",
    "$ark_root/libpandafile:libarkfile",
  ]

  cflags_cc = [ "-fvisibility=hidden" ]
}

ohos_shared_library("libark_ecma_debugger") {
  deps = [
    ":libark_ecma_debugger_static",
    "//ark/js_runtime:libark_jsruntime",
  ]

  install_enable = true
  part_name = "ark_js_runtime"

  output_extension = "so"
  if (!is_standard_system) {
    relative_install_dir = "ark"
  }
  subsystem_name = "ark"
}

source_set("libark_ecma_debugger_test_static") {
  sources = debugger_sources

  public_configs = [ ":ark_ecma_debugger_config" ]

  defines = [ "DEBUGGER_TEST" ]

  deps = [
    "$ark_root/libpandabase:libarkbase",
    "$ark_root/libpandafile:libarkfile",
  ]
}

ohos_shared_library("libark_ecma_debugger_test") {
  deps = [
    ":libark_ecma_debugger_test_static",
    "//ark/js_runtime:libark_jsruntime_test_static",
  ]

  if (!is_linux && !is_mingw && !is_mac) {
    if (build_public_version) {
      external_deps = [ "bytrace_standard:bytrace_core" ]
    }
  }

  public_configs = [ ":ark_ecma_debugger_config" ]

  output_extension = "so"
  subsystem_name = "test"
}
